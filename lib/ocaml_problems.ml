open Utils

module type Problem = sig
  val result : string
end

module Problem1 : Problem = struct
  (* If we list all the natural numbers below 10 that are multiples of 3 or 5,
  we get 3, 5, 6 and 9. The sum of these multiples is 23.

  Find the sum of all the multiples of 3 or 5 below 1000. *)
  let rec generator = function
    | 0 -> 0
    | n ->
        let rest = generator (n - 1) in
        if n mod 5 = 0 || n mod 3 = 0 then n + rest else rest

  let result = string_of_int (generator 999)
end

module Problem2 : Problem = struct
  (* Each new term in the Fibonacci sequence is generated by adding the
   previous two terms. By starting with 1 and 2, the first 10 terms will be:

                     1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

   By considering the terms in the Fibonacci sequence whose values do not
   exceed four million, find the sum of the even-valued terms.*)

  let rec generator sum x1 x2 =
    if x2 >= 4_000_000 then sum
    else
      let new_sum = sum + if x2 mod 2 = 0 then x2 else 0 in
      generator new_sum x2 (x1 + x2)

  let result = generator 0 1 1 |> string_of_int
end

module Problem3 : Problem = struct
  (*The prime factors of 13195 are 5, 7, 13 and 29.

  What is the largest prime factor of the number 600851475143 ?*)

  let number = 600851475143
  let start = [ number ]
  let intsq n = n |> float_of_int |> sqrt |> int_of_float

  let rec factorise n k =
    match k with
    | 1 -> None
    | _ -> if n mod k = 0 then Some (k, n / k) else factorise n (k - 1)

  let rec generator lst =
    let sorted_list = List.rev @@ List.sort ( - ) lst in
    match sorted_list with
    | [] -> None
    | m :: rest -> (
        let fact_res = factorise m (intsq m) in
        match fact_res with
        | None -> Some m
        | Some (f1, f2) -> generator (f1 :: f2 :: rest))

  let result =
    match generator start with
    | None -> 0 |> string_of_int
    | Some n -> n |> string_of_int
end

module Problem4 : Problem = struct
  (* A palindromic number reads the same both ways. The largest palindrome made
   from the product of two 2-digit numbers is 9009 = 91 Ã— 99.

   Find the largest palindrome made from the product of two 3-digit numbers.*)

  (* Solution: pure brute forcing, but making sure that no pair is computed more than once *)

  let all_pairs_betwwen n m = cartesian (n -- m) (n -- m)

  let result =
    all_pairs_betwwen 100 999
    |> List.filter (fun (a, b) -> a <= b)
    |> List.map (fun (a, b) -> a * b)
    |> List.filter is_palindrome |> List.fold_left max 0 |> string_of_int
end

module Problem5 : Problem = struct
  (* 2520 is the smallest number that can be divided by each of the numbers
   from 1 to 10 without any remainder.

   What is the smallest positive number that is evenly divisible by all of
   the numbers from 1 to 20? *)

  let result = 0 |> string_of_int
end

module Problem15 : Problem = struct

  let box_solution n = choose (n * 2) n
  let result = box_solution 20 |> string_of_int
end
